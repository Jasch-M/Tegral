plugins {
    id 'com.github.node-gradle.node'
    id 'base'
}

configurations {
    web {
        canBeConsumed = true
        canBeResolved = true
    }
}

def docsInputFiles = [
    'babel.config.js',
    'docusaurus.config.js',
    'sidebars.js'
]

def docsInputDirs = [
    'blog',
    'docs',
    'src',
    'static'
]

task pnpmInstallFrozen(type: PnpmTask) {
    args = [ 'install', '--frozen-lockfile' ]
    inputs.file('package.json')
        .withPropertyName("pkgjson-" + input)
        .withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.file('pnpm-lock.yaml')
        .withPropertyName("lockfile-" + input)
        .withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.dir('node_modules')

    outputs.cacheIf { true }
}

task cleanNodeModules {
    doLast {
        delete 'node_modules'
    }
}

task docusaurusStart(type: PnpmTask) {
    args = [ 'start' ]

    for (input in docsInputFiles) {
        inputs.file(input)
    }
    for (input in docsInputDirs) {
        inputs.files(input)
    }

    dependsOn pnpmInstallFrozen
}

task docusaurusBuild(type: PnpmTask) {
    args = [
        'build'
    ]

    for (input in docsInputFiles) {
        inputs.file(input)
            .withPropertyName("docsFile-" + input)
            .withPathSensitivity(PathSensitivity.RELATIVE)
    }
    for (input in docsInputDirs) {
        inputs.dir(input)
            .withPropertyName("docsDir-" + input)
            .withPathSensitivity(PathSensitivity.RELATIVE)
    }

    outputs.cacheIf { true }
    outputs.dir(layout.buildDirectory.dir('docusaurus'))
        .withPropertyName("result")

    dependsOn pnpmInstallFrozen
}

artifacts {
    web(layout.buildDirectory.dir("docusaurus")) {
        builtBy docusaurusBuild
    }
}
