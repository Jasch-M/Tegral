plugins {
    id 'version-catalog'
    id 'maven-publish'

    id 'guru.zoroark.tegral.base-conventions'
    id 'guru.zoroark.tegral.publish-conventions'
}

List<String> computeLibrariesToInclude() {
    def result = new ArrayList<String>()
    def bundles = new HashMap<String, ArrayList<String>>()

    def projects = rootProject.getSubprojects()
    for (project in projects) {
        if (project.extensions.extraProperties.properties.getOrDefault('includeInCatalog', false)) {
            def name = project.getName()
            if (name.startsWith("tegral-")) {
                name = name.substring(7)
            }

            result.add(new Tuple(name, project.group + ':' + project.name + ':' + project.version))

            def relevantBundles = project.getProperties().getOrDefault('includeInBundles', [])
            for (bundle in relevantBundles) {
                if (!bundles.containsKey(bundle)) {
                    bundles.put(bundle, new ArrayList<String>())
                }
                bundles.get(bundle).add(name)
            }
        }
    }
    return new Tuple(result, bundles)
}


def setupTask = task "setupVersionCatalog" {
    doLast {
        catalog {
            versionCatalog {
                def (exportedLibraries, exportedBundles) = computeLibrariesToInclude()
                for (exportedLibrary in exportedLibraries) {
                    def (name, dependency) = exportedLibrary
                    library(name, dependency)
                }

                print(exportedBundles)

                for (bundleEntry in exportedBundles.entrySet()) {
                    def bundleName = bundleEntry.getKey()
                    def bundleLibraries = bundleEntry.getValue()
                    bundle(bundleName, bundleLibraries)
                }
            }
        }
    }
}

tasks["generateCatalogAsToml"].dependsOn setupTask

publishing {
    publications {
        maven(MavenPublication) {
            from components.versionCatalog
        }
    }
}

ext.humanName = "Tegral Catalog"
ext.description = "Gradle Versions Catalog that makes it easy to get dependencies from the Tegral project"
ext.url = "https://tegral.zoroark.guru/docs/core/catalog/"
